resource "aws_sfn_state_machine" "on_file_created_handler" {
  name     = "${var.project-name}-sfn-on-file-created-handler"
  role_arn = aws_iam_role.on_file_created_handler.arn

  definition = jsonencode({
    Comment = "SFN to handle on-file-created event generated by EventBridge",
    StartAt = "GetObjectHeader",
    States = {
      GetObjectHeader = {
        Type = "Task",
        Arguments = {
          Bucket = "{% $states.input.bucket %}",
          Key    = "{% $states.input.key %}",
          Range  = "bytes=0-12"
        },
        Output = {
          refDate      = "{% $substring($states.result.Body, 0, 8) %}",
          documentType = "{% $substring($states.result.Body, 8, 1) %}",
          totalRecords = "{% $substring($states.result.Body, 9, 3) %}",
          bucket       = "{% $states.input.bucket %}",
          key          = "{% $states.input.key %}",
        },
        Resource = "arn:aws:states:::aws-sdk:s3:getObject",
        Next     = "FormatArguments"
      },
      FormatArguments = {
        Type = "Pass",
        Output = {
          refDate      = "{% $toMillis($states.input.refDate, '[Y0001][M01][D01]') ~> $fromMillis('[Y]-[M]-[D]') %}"
          documentType = "{% $states.input.documentType %}",
          totalRecords = "{% $number($states.input.totalRecords) %}",
          bucket       = "{% $states.input.bucket %}",
          key          = "{% $states.input.key %}",
          contextId    = "{% $split($states.context.Execution.Id, ':')[-1] %}",
          mappedTable  = {
            A = aws_glue_catalog_table.document_tables["document_a"].name,
            B = aws_glue_catalog_table.document_tables["document_b"].name,
          }
        }
        Next = "ProcessData"
      },
      ProcessData = {
        Type     = "Task",
        Resource = "arn:aws:states:::glue:startJobRun",
        Arguments = {
          JobName = aws_glue_job.process_data.name,
          Arguments = {
            "--REF_DATE"     = "{% $states.input.refDate %}",
            "--DOC_TYPE"     = "{% $states.input.documentType %}",
            "--CONTEXT_ID"   = "{% $states.input.contextId %}",
            "--INPUT_PATH"   = "{% 's3://' & $states.input.bucket & '/' & $states.input.key %}",
            "--OUTPUT_DB"    = aws_glue_catalog_database.database.name,
            "--OUTPUT_TABLE" = "{% $lookup($states.input.mappedTable, $states.input.documentType) %}"
          }
        },
        Output = {
          contextId = "{% $states.input.contextId %}",
          jobId     = "{% $states.result.JobRunId %}"
        },
        End = true
      }
    },
    QueryLanguage = "JSONata"
  })
}
