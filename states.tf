resource "aws_sfn_state_machine" "on_file_created_handler" {
  name     = "${var.project-name}-sfn-on-file-created-handler"
  role_arn = aws_iam_role.on_file_created_handler.arn

  definition = jsonencode({
    Comment = "SFN to handle on-file-created event generated by EventBridge",
    StartAt = "GetObjectHeader",
    States = {
      GetObjectHeader = {
        Type = "Task",
        Arguments = {
          Bucket = "{% $states.input.bucket %}",
          Key    = "{% $states.input.key %}",
          Range  = "bytes=0-12"
        },
        Output = {
          refDate      = "{% $substring($states.result.Body, 0, 8) %}",
          documentType = "{% $substring($states.result.Body, 8, 1) %}",
          totalRecords = "{% $substring($states.result.Body, 9, 3) %}",
          bucket       = "{% $states.input.bucket %}",
          key          = "{% $states.input.key %}",
        },
        Resource = "arn:aws:states:::aws-sdk:s3:getObject",
        Next     = "FormatArguments"
      },
      FormatArguments = {
        Type = "Pass",
        Output = {
          refDate      = "{% $toMillis($states.input.refDate, '[Y0001][M01][D01]') ~> $fromMillis('[Y]-[M]-[D]') %}"
          documentType = "{% $states.input.documentType %}",
          totalRecords = "{% $number($states.input.totalRecords) %}",
          bucket       = "{% $states.input.bucket %}",
          key          = "{% $states.input.key %}",
        }
        Next = "GetObjectRecords"
      },
      GetObjectRecords = {
        Type = "Task",
        Arguments = {
          Bucket = "{% $states.input.bucket %}",
          Key    = "{% $states.input.key %}",
          Range  = "bytes=12-"
        },
        Output = {
          refDate      = "{% $states.input.refDate %}",
          documentType = "{% $states.input.documentType %}",
          totalRecords = "{% $states.input.totalRecords %}",
          # Converts each line of a string spliting by '\n' into a list of objects. Ex:
          # "row1\nrow2\n" -> [{"recordRow": "row1"}, {"recordRow": "row2"}]
          records = "{% $map($split($states.result.Body, '\n')[$boolean($)], function($v){ {'recordRow': $v} }) %}",
        },
        Resource = "arn:aws:states:::aws-sdk:s3:getObject",
        Next     = "ProcessData",
      },
      ProcessData = {
        Type = "Choice",
        Choices = [
          { Condition = "{% $states.input.documentType = 'A' %}", Next = "ProcessTypeA" },
          { Condition = "{% $states.input.documentType = 'B' %}", Next = "ProcessTypeB" },
        ]
        Default = "UnrecognizedDocumentType"
      },
      ProcessTypeA = {
        Type  = "Map",
        Items = "{% $states.input.records %}"
        ItemSelector = {
          index  = "{% $states.context.Map.Item.Index %}",
          record = "{% $states.context.Map.Item.Value.recordRow %}",
        },
        ItemProcessor = {
          ProcessorConfig = {
            Mode = "INLINE"
          },
          StartAt = "ProcessA",
          States = {
            ProcessA = {
              Type = "Pass"
              Output = {
                userId = "{% $substring($states.input.record, 0, 12) %}",
                value  = "{% $number($substring($states.input.record, 12, 7)) / 100 %}",
              }
              End = true
            }
          }
        },
        Output = {
          processedRecords = "{% $states.result %}",
          refDate          = "{% $states.input.refDate %}",
          documentType     = "{% $states.input.documentType %}",
        },
        Next = "PutObject",
      },
      ProcessTypeB = {
        Type  = "Map",
        Items = "{% $states.input.records %}"
        ItemSelector = {
          index  = "{% $states.context.Map.Item.Index %}",
          record = "{% $states.context.Map.Item.Value.recordRow %}",
        },
        ItemProcessor = {
          ProcessorConfig = {
            Mode = "INLINE"
          },
          StartAt = "ProcessB",
          States = {
            ProcessB = {
              Type = "Pass"
              Output = {
                sourceId   = "{% $substring($states.input.record, 0, 12) %}",
                targetId   = "{% $substring($states.input.record, 12, 12) %}",
                value      = "{% $number($substring($states.input.record, 24, 7)) / 100 %}",
                isSchedule = "{% $substring($states.input.record, 31, 1) = 'T' %}",
              }
              End = true
            }
          }
        }
        Output = {
          processedRecords = "{% $states.result %}",
          refDate          = "{% $states.input.refDate %}",
          documentType     = "{% $states.input.documentType %}",
        },
        Next = "PutObject",
      },
      PutObject = {
        Type     = "Task",
        Resource = "arn:aws:states:::aws-sdk:s3:putObject",
        Arguments = {
          # Body   = "{% $join($map($states.input.processedRecords,  $string), '\n') %}",
          # Body   = "{\"index\": \"a\"}\n{\"index\": \"b\"}\n"
          Body   = "{% $states.input.processedRecords %}"
          Bucket = aws_s3_bucket.processed_data_bucket.id,
          Key    = "{% $toMillis($states.input.refDate, '[Y]-[M]-[D]') ~> $fromMillis('ano=[Y]/mes=[M]/dia=[D]/' & $states.input.documentType & '.json') %}",
        },
        End = true
      },
      UnrecognizedDocumentType = {
        Type  = "Fail",
        Error = "UnrecognizedDocumentType",
        Cause = "Document Type is not supported"
      },
    },
    QueryLanguage = "JSONata"
  })
}
