resource "aws_sfn_state_machine" "on_file_created_handler" {
  name     = "${var.project-name}-sfn-on-file-created-handler"
  role_arn = aws_iam_role.on_file_created_handler.arn

  definition = jsonencode({
    Comment = "SFN to handle on-file-created event generated by EventBridge",
    StartAt = "GetObject",
    States = {
      GetObject = {
        Type = "Task",
        Arguments = {
          Bucket = "{% $states.input.bucket %}",
          Key    = "{% $states.input.key %}",
          Range  = "bytes=0-12"
        },
        Output = {
          refDate      = "{% $substring($states.result.Body, 0, 8) %}",
          dataType     = "{% $substring($states.result.Body, 8, 1) %}",
          totalRecords = "{% $substring($states.result.Body, 9, 3) %}",
        },
        Resource = "arn:aws:states:::aws-sdk:s3:getObject",
        Next     = "FormatArguments"
      },
      FormatArguments = {
        Type = "Pass",
        Output = {
          refDate      = "{% $toMillis($states.input.refDate, '[Y0001][M01][D01]') ~> $fromMillis('[Y]-[M]-[D]') %}"
          dataType     = "{% $states.input.dataType %}",
          totalRecords = "{% $number($states.input.totalRecords) %}",
        }
        Next = "ProcessData"
      },
      ProcessData = {
        Type = "Choice",
        Choices = [
          { Condition = "{% $states.input.dataType = 'A' %}", Next = "ProcessTypeA" },
          { Condition = "{% $states.input.dataType = 'B' %}", Next = "ProcessTypeB" },
        ]
        Default = "UnrecognizedDocumentType"
      },
      ProcessTypeA = {
        Type = "Pass",
        End  = true,
      },
      ProcessTypeB = {
        Type = "Pass",
        End  = true,
      },
      UnrecognizedDocumentType = {
        Type  = "Fail",
        Error = "UnrecognizedDocumentType",
        Cause = "Document Type is not supported"
      },
    },
    QueryLanguage = "JSONata"
  })
}
